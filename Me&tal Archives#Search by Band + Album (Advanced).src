# ###################################################################
#
#     _____          __         .__           _____                .__    .__
#    /     \   _____/  |______  |  |         /  _  \_______   ____ |  |__ |__|__  __ ____   ______
#   /  \ /  \_/ __ \   __\__  \ |  |        /  /_\  \_  __ \_/ ___\|  |  \|  \  \/ // __ \ /  ___/
#  /    Y    \  ___/|  |  / __ \|  |__     /    |    \  | \/\  \___|   Y  \  |\   /\  ___/ \___ \ 
#  \____|__  /\___  >__| (____  /____/     \____|__  /__|    \___  >___|  /__| \_/  \___  >____  >
#          \/     \/          \/                   \/            \/     \/              \/     \/ 
#                     
#
#
# Mp3tag parsing for Metal-Archives.com, created by dano on 2007-12-15
#
# This file should be in your sources dir. 
# On Windows XP it's C:\Documents and Settings\*username*\Application Data\Mp3tag\data\sources
#
# This config is used to gain informations about an ALBUM.
#
#   2009-07-07: Update
#   2011-04-29: Update
#
# Only search for Metal music, normal music is not on the site
# Also only real Metal. KoRn, Linkin Park or similar is not on the site
#

[Name]=The Metal Archives
[BasedOn]=www.metal-archives.com
[IndexUrl]=https://www.metal-archives.com/search/ajax-advanced/searching/albums/?releaseYearFrom=0001&releaseYearTo=9999&sEcho=1&iColumns=5&exactBandMatch=1&releaseFormat%5B%5D=CD&releaseFormat%5B%5D=Cassette*&releaseFormat%5B%5D=Vinyl*&releaseFormat%5B%5D=VHS&releaseFormat%5B%5D=DVD&releaseFormat%5B%5D=Digital&releaseFormat%5B%5D=Blu-ray*&releaseFormat%5B%5D=Other
[AlbumUrl]=
[WordSeparator]=+
[IndexFormat]=%Artist%|%_url%|%Album%|%Type%|%Year%|%Catalog%|%Format%
[SearchBy]=Band||%artist%||&bandName=%s||Album||%album%||&releaseTitle=%s
[Encoding]=url-utf-8
[UserAgent]=1

[ParserScriptIndex]=...
# ###################################################################
#                   I  N  D  E  X
# ###################################################################
# debug "on" "D:\Projects\mp3tag_temp\debug_MA_Index.txt"
# debugwriteinput "D:\Projects\mp3tag_temp\debug_MA_Index_Input.txt"

findline "<a href=\\\""
do
    # Artist 
    findinline "\\\">"
    sayuntil "<"
    say "|"
    
    # Url
    moveline 1
    findinline "<a href=\\\"" 
    sayuntil "\\\""
    say "|"
    
    # Album
    findinline "\\\">"  
    sayuntil "<"
    say "|"
    
    # Type
    moveline 1
    replace "\"" ""
    replace "," ""
    sayrest
    say "|"
    
    # Year
    moveline 1
    replace "\"" ""
    sayuntil "<"
    say "|"
    
    # Catalog
    moveline 1
    replace "\"" ""
    replace "," ""
    sayuntil "<"
    say "|"
    
    # Media Type
    moveline 1
    replace "\"" ""
    sayuntil "]"
    saynewline
     
    findline "<a href=\\\"" 1 1
    unspace
while "\"<a href" 500

[ParserScriptAlbum]=...
# ###################################################################
#                   A  L  B  U  M
# ###################################################################
# debug "on" "D:\Projects\mp3tag_temp\debug_MA_Album.txt"
# debugwriteinput "D:\Projects\mp3tag_temp\debug_MA_Album_Input.txt"

findline "id=\"album_sidebar\">"

# Band URL
outputto "METAL ARCHIVES BAND URL"
findline "<a href=\""
replace "#band_tab_discography" ""
findinline "<a href=\""
sayuntil "\""

# Coverurl
outputto "coverurl"
findline "class=\"album_img\"" 1 1
unspace
if "<div class=\"album_img\">"
    joinuntil "</div>"
    findinline " href=\"" 1 1
    sayuntil "\""
endif
gotoline 1

# Album
outputto "Album"
findline "<div id=\"album_info\">"
findline "<h1 class=\"album_name\">" 1 1
unspace
if "<h1 class=\"album_name\">"
    regexpreplace "<a[^>]+>" ""
    findinline "<h1 class=\"album_name\">"
    sayuntil "</"
else
    gotoline 1
    findline "<h1 class=\"album_name noCaps\">" 1 1
    # if "<h1 class=\"album_name noCaps\">"
        regexpreplace "<a[^>]+>" ""
        findinline "<h1 class=\"album_name noCaps\">"
        sayuntil "</"
    # else
    #     say ""
    # endif
endif

# Artist
outputto "Artist"
findline "<h2 class=\"band_name\">"
moveline 1 1
regexpreplace "<a[^>]+>" ""
sayuntil "</"

# Type
outputto "METAL ARCHIVES TYPE"
findline "<dt>Type:</dt>"
moveline 1 1
regexpreplace "<[^>]+>" "" #kt
sayrest

# Year            
outputto "Year"
findline "<dt>Release date:</dt>"
moveline 1 1
regexpreplace "<[^>]+>" "" #kt
sayregexp "\d\d\d\d"

# Date
outputto "METAL ARCHIVES DATE"
replace "January " "-01-"
replace "February " "-02-"
replace "March " "-03-"
replace "April " "-04-"
replace "May " "-05-"
replace "June " "-06-"
replace "July " "-07-"
replace "August " "-08-"
replace "September " "-09-"
replace "October " "-10-"
replace "November " "-11-"
replace "December " "-12-"
unspace
regexpreplace "(\d+)[a-z,]+\s*" "$1-"
regexpreplace "-(\d)-" "-0$1-"
regexpreplace "^(\d\d\d\d)$" "-01-01-$1"
regexpreplace "^-(\d\d)-(\d\d\d\d)$" "-$1-01-$2"
regexpreplace "-(\d\d)-(\d\d)-(\d\d\d\d)" "$3-$1-$2"
sayregexp "\d\d\d\d-\d\d-\d\d"

# Catalog ID
outputto "Catalog"
findline "<dt>Catalog ID:</dt>"
moveline 1 1
regexpreplace "<[^>]+>" "" #kt
sayrest

# Version information
outputto "METAL ARCHIVES EDITION"
findline "<dt>Version desc.:</dt>" 1 1
unspace
if "<dt>Version desc.:</dt>"
    moveline 1 1
    regexpreplace "<[^>]+>" "" #kt
    sayrest
else
    gotoline 1
    findline "id=\"album_sidebar\">"
endif
            
# Publisher
outputto "Publisher"
findline "<dt>Label:</dt>"
moveline 1 1
regexpreplace "<[^>]+>" "" #kt
sayrest

# Album Rating
outputto "METAL ARCHIVES RATING"
findline "<dt>Reviews:</dt>"
moveline 1 1
joinuntil "</dd>"
regexpreplace "<[^>]+>" "" #kt
regexpreplace "\d+ reviews? (\(avg\.)? *" ""
replace ")" ""
sayrest

# Tracks Data
set "TEMP_DiscNumber" "1"
findline "class=\"display table_lyrics\""
do
    # Disc Number
    trim "on"
    findline "<tr class="
    unspace
    if "<tr class=\"discRow\">"
        trim "on"
        findline "<td colspan=\"4\">"
        joinuntil "</td>"
        unspace
        set "TEMP_DiscNumber"
        outputto "TEMP_DiscNumber"
        unspace
        sayregexp "\s\d\s"

        outputto "Discnumber"
        unspace
        sayoutput "TEMP_DiscNumber"
        say "|"
    else
        outputto "Discnumber"
        unspace
        sayoutput "TEMP_DiscNumber"
        say "|"
    endif
    
    findline "<td width=\"20\">" 1 1
    unspace
    if "<td width=\"20\">"
        findline "<td" 2

        # Track Title
        outputto "Tracks"
        joinuntil "</td>"
        regexpreplace "[^\S\r\n]{2,}" " "
        findinline "<td"
        findinline ">"
        sayuntil "</td>"
        say "|"

        gotochar 1

        # Track "Bonus" Flag
        outputto "METAL ARCHIVES BONUS"
        unspace
        if "<td class=\"wrapWords bonus\">"
            say "1|"
        else
            say "|"
        endif
        
        # Length
        outputto "_length"
        findline "align=\"right\">"
        sayregexp "\d+:\d+"
        say "|"

        # Track "Instrumental" flag
        outputto "METAL ARCHIVES INSTRUMENTAL"
        findline "<td nowrap=\"nowrap\">&nbsp;" 1 1
        unspace
        if "<td nowrap=\"nowrap\">&nbsp;"
            moveline 1 1
            unspace
            if "<em>instrumental</em>"
                # findinline "<em"
                # findinline ">"
                # sayuntil "</em>"
                # say "|"
                say "1|"
            else
                findinline "=\"\">" 1 1
                if "Instrumental</a>"
                    say "1|"
                else
                    say "|"
                endif
            endif
        endif
        
        findline "<tr class=" 1 1
        unspace
    endif
while "<tr class=" 666

gotoline 1
      
# METAL ARCHIVE INFO
findline "<div id=\"album_tabs_notes\"" 1 1
unspace
if "<div id="
    outputto "METAL ARCHIVES INFO"
    findline "class=\"ui-tabs-panel-content"
    moveline 1 1
    joinuntil "</div>"      
    
    replace "<i>" ""
    replace "</i>" ""
    replace "</td>" ""
    replace "</a>" ""
    replace "<b>" ""
    replace "</b>" ""
    
    regexpreplace "[^\S\r\n]{2,}" ""
    regexpreplace "<p[^>]*>" ""
    regexpreplace "</p>" "\r\n\r\n"
    regexpreplace "<a href[^>]*>" ""
    
    regexpreplace ">\r" ">"
    regexpreplace "\r<" "<"
    regexpreplace "(?i)<br>|<br />" "\r\n"
    regexpreplace "(?i)<br>|<br/>" "\r\n"

    sayuntil "</div>"
endif

outputto "METAL ARCHIVES ALBUM URL"
sayoutput "CurrentUrl"

# ... Customization ...
# Fields that you want to remove
#   (to remove a field write: set "field")
set "TEMP_DiscNumber"
set "ITEMP_DiscNumber"