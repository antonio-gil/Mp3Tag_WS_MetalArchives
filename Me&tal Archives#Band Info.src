# ###################################################################
#
#     _____          __         .__           _____                .__    .__
#    /     \   _____/  |______  |  |         /  _  \_______   ____ |  |__ |__|__  __ ____   ______
#   /  \ /  \_/ __ \   __\__  \ |  |        /  /_\  \_  __ \_/ ___\|  |  \|  \  \/ // __ \ /  ___/
#  /    Y    \  ___/|  |  / __ \|  |__     /    |    \  | \/\  \___|   Y  \  |\   /\  ___/ \___ \ 
#  \____|__  /\___  >__| (____  /____/     \____|__  /__|    \___  >___|  /__| \_/  \___  >____  >
#          \/     \/          \/                   \/            \/     \/              \/     \/ 
#                     
#
#
# Mp3tag parsing for Metal-Archives.com, created by dano on 2007-12-15 - modified by camzo on 11-09-2017 & 02-12-2017
#
# This file should be in your sources dir. 
# On Windows XP it's C:\Documents and Settings\*username*\Application Data\Mp3tag\data\sources
#
# This config is used to gain informations about an ALBUM.
#
#	2009-07-07: Update
#	2011-04-29: Update
#	2017-12-02: Updated again, now shows lists similar to album script, should stop wrong bands with same name being selected.
#	2018-11-24: Changed the index URL as it was causing some 404 errors.
#
# Only search for Metal music, normal music is not on the site
# Also only real Metal. KoRn, Linkin Park or similar is not on the site
#

[Name]=The Metal Archives
[BasedOn]=www.metal-archives.com
[IndexUrl]=https://www.metal-archives.com/search/ajax-advanced/searching/bands/?genre=&country=&yearCreationFrom=&yearCreationTo=&bandNotes=&status=&themes=&location=&bandLabelName=&sEcho=1&iColumns=3&sColumns=&iDisplayStart=0&iDisplayLength=200&exactBandMatch=1&bandName=%s
# [IndexUrl]=https://www.metal-archives.com/search/ajax-advanced/searching/bands?bandName=%s
[AlbumUrl]=
[WordSeparator]=+
[IndexFormat]=%Artist%|%_url%|%Genre%|%Country%
[SearchBy]=%artist%
[Encoding]=url-utf-8

[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################
# debug "on" "D:\Projects\mp3tag_temp\debug_MA_Band_Index.txt"
# debugwriteinput "D:\Projects\mp3tag_temp\debug_MA_Band_Index_Input.txt"

findline "<a href=\\"
do
	# Artist
	findinline ">" 1 1
	findinline ""
	sayuntil "</a"
	say "|"
	
	# Url 
	MoveLine -1
	MoveLine 1
	findinline "<a"
	findinline "href=\\"
	MoveChar 1
	sayuntil "\\"
	say "|"
	
	#Genre
	MoveLine 1
	replace "\"" ""
    replace "," ""
	sayuntil ","
	Say "|"
	
	#Country
	MoveLine 1
	replace "\"" ""
    replace "," ""
	sayuntil "]"
	saynewline
	
	
	findline "<a href=\\" 1 1
    unspace
	
while "\"<a href" 500
	
	
[ParserScriptAlbum]=...
# ###################################################################
#					B  A  N  D
# ###################################################################
# debug "on" "D:\Projects\mp3tag_temp\debug_MA_Band.txt"
# debugwriteinput "D:\Projects\mp3tag_temp\debug_MA_Band_Input.txt"


findline "<dl class="float_left">"

# COUNTRY
outputto "Country"
findline "<dd><a href="https://www.metal-archives.com/lists"
findinline "https://www.metal-archives.com/lists/"
findinline ">" 1 1
sayuntil "</a"

# LOCATION
outputto "METAL ARCHIVES LOCATION"
findline "<dt>Location:</dt>"
findline "<dd>"
findinline "dd>"
sayuntil "</dd>"

# ACTIVE?
outputto "METAL ARCHIVES STATUS"
findline "<dt>Status:</dt>"
findline "<dd class="
findinline ">"
sayuntil "</dd"

#FORMATION YEAR
outputto "METAL ARCHIVES FORMATION YEAR"
findline "<dt>Formed in:</dt>"
findline "<dd>"
findinline ">"
sayuntil "</"

#GENRE
outputto "Genre"
findline "<dt>Genre:</dt>"
findline "<dd>"
findinline "<dd>"
sayuntil "</dd>"

#LYRICAL THEMES
outputto "METAL ARCHIVES LYRICAL THEMES"
findline "<dt>Themes:</dt>"
findline "<dd>"
findinline "<dd>"
sayuntil "</dd>"
